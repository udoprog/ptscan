// vim: ft=rust
use crate::{
    Value,
    filter::{
        lexer::Token,
        ast::*
    }
};

grammar<'input>;

pub Or: Expression = {
    <left:And> <rest:(or And)+> => {
        let mut or = Vec::new();
        or.push(left);
        or.extend(rest.into_iter().map(|(_, r)| r));
        Expression::Or(or)
    },

    And,
}

And: Expression = {
    <left:Filter> <rest:(and Filter)+> => {
        let mut and = Vec::new();
        and.push(left);
        and.extend(rest.into_iter().map(|(_, r)| r));
        Expression::And(and)
    },

    Filter,
}

Filter: Expression = {
    changed => Expression::Changed,
    same => Expression::Same,
    inc => Expression::Inc,
    dec => Expression::Dec,
    "$value" "==" <value:literal> => Expression::Eq(value),
    "$value" "!=" <value:literal> => Expression::Neq(value),
    "$value" "<=" <value:literal> => Expression::Lte(value),
    "$value" ">=" <value:literal> => Expression::Gte(value),
    "$value" "<" <value:literal> => Expression::Lt(value),
    "$value" ">" <value:literal> => Expression::Gt(value),
};

extern {
    type Location = usize;
    type Error = crate::filter::lexer::Error;

    enum Token {
        "$value" => Token::Value,
        changed => Token::Changed,
        same => Token::Same,
        inc => Token::Inc,
        dec => Token::Dec,
        and => Token::And,
        or => Token::Or,
        "==" => Token::Eq,
        "!=" => Token::Neq,
        "<=" => Token::Lte,
        ">=" => Token::Gte,
        "<" => Token::Lt,
        ">" => Token::Gt,
        "(" => Token::OpenParen,
        ")" => Token::CloseParen,
        literal => Token::Literal(<Value>),
    }
}
