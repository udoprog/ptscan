// vim: ft=rust
use crate::{
    Offset,
    pointer::{PortableBase, PortablePointer, lexer::{Hex, Token}},
};

grammar<'input>;

pub Pointer: PortablePointer = {
    <base:PortableBase> <offsets:("->" <Deref>)*> => {
        PortablePointer {
            base,
            offsets: offsets.into_iter().map(Hex::into_offset).collect(),
        }
    },
}

Deref: Hex = {
    <hex:Hex> => hex,
    "+" <hex:Hex> => hex,
    "-" <hex:Hex> => hex.negate(),
}

PortableBase: PortableBase = {
    <address:Hex> <offsets:BaseOffset*> => {
        let mut address = address.into_address();

        for o in offsets {
            address = address.saturating_offset(o);
        }

        PortableBase::Address { address }
    },
    <name:String> <offsets:BaseOffset*> => {
        let mut offset = Offset::zero();

        for o in offsets {
            offset = offset.saturating_add(o);
        }

        PortableBase::Module { name, offset }
    },
}

BaseOffset: Offset = {
    "+" <hex:Hex> => hex.into_offset(),
    "-" <hex:Hex> => hex.negate().into_offset(),
}

extern {
    type Location = usize;
    type Error = crate::pointer::lexer::Error;

    enum Token {
        Hex => Token::Hex(<Hex>),
        String => Token::String(<String>),
        "+" => Token::Plus,
        "-" => Token::Minus,
        "->" => Token::Rocket,
    }
}
