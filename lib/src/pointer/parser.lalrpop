// vim: ft=rust
use crate::{
    address::Offset,
    pointer::{Pointer, PointerBase, lexer::{Hex, Token}},
};

grammar<'input>;

pub Pointer: Pointer = {
    <base:PointerBase> <offsets:("->" <Deref>)*> => {
        Pointer::new(base, offsets.into_iter().map(Hex::into_offset), None)
    },
}

Deref: Hex = {
    <hex:Hex> => hex,
    "+" <hex:Hex> => hex,
    "-" <hex:Hex> => hex.negate(),
}

PointerBase: PointerBase = {
    <address:Hex> <offsets:BaseOffset*> => {
        let mut address = address.into_address();

        for o in offsets {
            address = address.saturating_offset(o);
        }

        PointerBase::Address { address }
    },
    <name:String> <offsets:BaseOffset*> => {
        let mut offset = Offset::zero();

        for o in offsets {
            offset = offset.saturating_add(o);
        }

        PointerBase::Module { name, offset }
    },
}

BaseOffset: Offset = {
    "+" <hex:Hex> => hex.into_offset(),
    "-" <hex:Hex> => hex.negate().into_offset(),
}

extern {
    type Location = usize;
    type Error = crate::pointer::lexer::Error;

    enum Token {
        Hex => Token::Hex(<Hex>),
        String => Token::String(<String>),
        "+" => Token::Plus,
        "-" => Token::Minus,
        "->" => Token::Rocket,
    }
}
